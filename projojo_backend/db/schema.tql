define

entity user @abstract,
    owns id @key,
    owns email @card(1),
    owns fullName @card(1),
    owns imagePath @card(1),
    plays oauthAuthentication:user @card(1..10),
    plays statusChangeRequest:requester @card(0..),
    plays statusChangeRequest:responder @card(0..);

entity supervisor sub user,
    plays manages:supervisor @card(1),
    plays creates:supervisor @card(0..);

entity student sub user,
    owns description @card(0..1),
    owns cvPath @card(0..1),
    plays hasSkill:student @card(0..),
    plays registersForTask:student @card(0..),
    plays hasPortfolio:student @card(0..);

entity teacher sub user;

entity oauthProvider,
    owns name @key, # e.g. "microsoft", "google"
    plays oauthAuthentication:provider;

entity business,
    owns id @key,
    owns name @card(1), # was `name @key` before UUID migration
    owns description @card(1),
    owns imagePath @card(1),
    owns location @card(1),
    owns country @card(0..1),
    owns sector @card(0..1),
    owns companySize @card(0..1),
    owns website @card(0..1),
    owns isArchived @card(0..1),
    plays manages:business @card(0..),
    plays hasProjects:business @card(0..),
    plays businessInvite:business @card(0..);

entity project,
    owns id @key,
    owns name @card(1),
    owns description @card(1),
    owns imagePath @card(1),
    owns createdAt @card(1),
    owns location @card(0..1),
    owns isArchived @card(0..1),
    owns isPublic @card(0..1),
    owns impactSummary @card(0..1),
    owns startDate @card(0..1),
    owns endDate @card(0..1),
    plays hasProjects:project @card(1),
    plays containsTask:project @card(0..),
    plays creates:project @card(0..),
    plays hasTheme:project @card(0..);

# Thema's voor publieke discovery (gebaseerd op SDG's of eigen thema's)
entity theme,
    owns id @key,
    owns name @card(1),
    owns sdgCode @card(0..1),      # Optionele SDG koppeling (bijv. "SDG3", "SDG13")
    owns icon @card(0..1),          # Material icon name
    owns themeDescription @card(0..1),
    owns color @card(0..1),         # Kleurcode (bijv. "#4CAF50")
    owns displayOrder @card(0..1),  # Sorteervolgorde
    plays hasTheme:theme @card(0..);

entity task,
    owns id @key,
    owns name @card(1),
    owns description @card(1),
    owns totalNeeded @card(1),
    owns createdAt @card(1),
    owns startDate @card(0..1),
    owns endDate @card(0..1),
    plays containsTask:task @card(1),
    plays requiresSkill:task @card(0..),
    plays registersForTask:task @card(0..);

entity skill,
    owns id @key,
    owns name @card(1) @unique, # was `name @key` before UUID migration
    owns isPending @card(1),
    owns createdAt @card(1),
    plays requiresSkill:skill @card(0..),
    plays hasSkill:skill @card(0..);

entity inviteKey,
    owns key @key,
    owns inviteType @card(1),
    owns isUsed @card(1),
    owns createdAt @card(1),
    plays businessInvite:key @card(0..1);

relation oauthAuthentication,
    relates provider,
    relates user,
    owns oauthSub @key;

relation creates,
    relates supervisor @card(1),
    relates project @card(0..),
    owns createdAt @card(1);

relation manages,
    relates supervisor @card(1),
    relates business @card(1),
    owns location @card(1..);

relation hasProjects,
    relates business @card(1),
    relates project @card(1..);

relation containsTask,
    relates project @card(1),
    relates task @card(1);

relation requiresSkill,
    relates task @card(1),
    relates skill @card(1);

relation hasSkill,
    relates student @card(1),
    relates skill @card(1),
    owns description @card(0..1);

relation registersForTask,
    relates student @card(1),
    relates task @card(1),
    owns description @card(1),
    owns isAccepted @card(0..1),
    owns response @card(0..1),
    owns createdAt @card(1),
    owns requestedAt @card(0..1),
    owns acceptedAt @card(0..1),
    owns startedAt @card(0..1),
    owns completedAt @card(0..1),
    owns registrationStatus @card(0..1),
    plays statusChangeRequest:registration @card(0..);

relation businessInvite,
    relates business @card(1),
    relates key @card(1);

entity portfolioItem,
    owns id @key,
    owns timelineSnapshot @card(1),
    owns projectSnapshot @card(1),
    owns taskSnapshot @card(1),
    owns skillsSnapshot @card(1),
    plays hasPortfolio:item @card(1);

relation hasPortfolio,
    relates student @card(1),
    relates item @card(1);

# Project-Thema koppeling
relation hasTheme,
    relates project @card(1),
    relates theme @card(1);

# Status change consensus - per taak-registratie
relation statusChangeRequest,
    relates registration @card(1),      # de registersForTask die beoordeeld wordt
    relates requester @card(0..1),      # user die initieert (leeg bij auto end_review)
    relates responder @card(0..1),      # user die reageert
    owns id @key,
    owns requestType @card(1),          # "completion" / "cancellation" / "end_review"
    owns reason @card(1),
    owns requestStatus @card(1),        # "pending" / "approved" / "denied" / "auto_approved"
    owns createdAt @card(1),
    owns respondedAt @card(0..1),
    owns responseMessage @card(0..1),
    owns autoApproveAt @card(0..1);     # Taak endDate + 14 dagen (alleen bij end_review)

#Werkt helaas nog niet :(
# TLDR: maakt automatisch een permission relatie aan als een supervisor een project aanmaakt permissie voor wat? zien we dan wel maar wss aanpassingen doen etc
# nu ik erover nadenk moet dit eigenlijk gebeuren met projectCreation.. TODO: apply rule for projectCreation relation
#rule access: when {
#    (supervisor: $s, project: $p) isa projectCreation;
#} then {
#    (supervisorRole: $s, projectRole: $p) isa accessPermission;
#};


attribute email value string;
attribute name value string;
attribute fullName value string;
attribute imagePath value string;
attribute cvPath value string;
attribute id value string;
attribute oauthSub value string;
attribute description value string;
attribute location value string;
attribute country value string;
attribute isPending value boolean;
attribute createdAt value datetime-tz;
attribute totalNeeded value integer;
attribute isAccepted value boolean;
attribute response value string;
attribute key value string;
attribute inviteType value string @values("business", "teacher");
attribute isUsed value boolean;
attribute sector value string;
attribute companySize value string;
attribute website value string;
attribute isArchived value boolean;
attribute requestedAt value datetime-tz;
attribute acceptedAt value datetime-tz;
attribute startedAt value datetime-tz;
attribute completedAt value datetime-tz;
attribute timelineSnapshot value string;
attribute projectSnapshot value string;
attribute taskSnapshot value string;
attribute skillsSnapshot value string;
attribute startDate value datetime-tz;
attribute endDate value datetime-tz;
attribute isPublic value boolean;
attribute impactSummary value string;
attribute sdgCode value string;
attribute icon value string;
attribute themeDescription value string;
attribute color value string;
attribute displayOrder value integer;
attribute reason value string;
attribute requestType value string @values("completion", "cancellation", "end_review");
attribute requestStatus value string @values("pending", "approved", "denied", "auto_approved");
attribute responseMessage value string;
attribute autoApproveAt value datetime-tz;
attribute respondedAt value datetime-tz;
attribute registrationStatus value string @values("lopend", "afgerond", "afgebroken");