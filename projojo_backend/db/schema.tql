define

entity user @abstract,
    owns email @key,
    owns imagePath @card(1) @unique,
    owns fullName @card(1);

entity supervisor sub user,
    plays authentication:user @card(1..10),
    plays businessAssociation:supervisor @card(1);

entity student sub user,
    owns schoolAccountName @card(1);

entity teacher sub user,
    owns schoolAccountName @card(1);

entity identityProvider,
    owns name @card(0..1),
    plays authentication:identityProvider;

entity business,
    owns name @key, 
    owns description @card(1),
    owns imagePath @card(1),
    owns location @card(1..),
    plays businessAssociation:business @card(1..),
    plays businessProjects:business @card(0..1);

entity project,
    owns name @card(1),
    owns description @card(1),
    owns imagePath @card(1),
    owns createdAt @card(1),
    plays businessProjects:project @card(1),
    plays projectTask:project @card(0..);

entity task,
    owns name @card(1),
    owns description @card(1),
    owns totalNeeded @card(1),
    owns createdAt @card(1),
    plays projectTask:task @card(1),
    plays taskSkill:task @card(0..);

entity skill,
    owns name @key,
    owns isPending @card(1),
    owns createdAt @card(1),
    plays taskSkill:skill @card(0..);

relation authentication,
    relates user,
    relates identityProvider,
    owns username @card(1),
    owns id @key;
    
relation businessAssociation,
    relates supervisor @card(1),
    relates business @card(1);

relation businessProjects,
    relates business @card(1),
    relates project @card(1..);

relation projectTask,
    relates project @card(1),
    relates task @card(1);

relation taskSkill,
    relates task @card(1),
    relates skill @card(1);

attribute email value string;
attribute name value string;
attribute fullName value string;
attribute imagePath value string;
attribute schoolAccountName value string;
attribute username value string;
attribute id value string;
attribute description value string;
attribute location value string;
attribute isPending value boolean;
attribute createdAt value datetime;
attribute totalNeeded value integer;