define

entity user @abstract,
    owns id @key,
    owns email @card(1),
    owns fullName @card(1),
    owns imagePath @card(1),
    plays oauthAuthentication:user @card(1..10);

entity supervisor sub user,
    plays manages:supervisor @card(1),
    plays creates:supervisor @card(0..);

entity student sub user,
    owns description @card(0..1),
    owns cvPath @card(0..1),
    plays hasSkill:student @card(0..),
    plays registersForTask:student @card(0..),
    plays hasPortfolio:student @card(0..);

entity teacher sub user;

entity oauthProvider,
    owns name @key, # e.g. "microsoft", "google"
    plays oauthAuthentication:provider;

entity business,
    owns id @key,
    owns name @card(1), # was `name @key` before UUID migration
    owns description @card(1),
    owns imagePath @card(1),
    owns location @card(1),
    owns country @card(0..1),
    owns sector @card(0..1),
    owns companySize @card(0..1),
    owns website @card(0..1),
    owns isArchived @card(0..1),
    plays manages:business @card(0..),
    plays hasProjects:business @card(0..),
    plays businessInvite:business @card(0..);

entity project,
    owns id @key,
    owns name @card(1),
    owns description @card(1),
    owns imagePath @card(1),
    owns createdAt @card(1),
    owns location @card(0..1),
    owns isArchived @card(0..1),
    owns startDate @card(0..1),
    owns endDate @card(0..1),
    plays hasProjects:project @card(1),
    plays containsTask:project @card(0..),
    plays creates:project @card(0..);

entity task,
    owns id @key,
    owns name @card(1),
    owns description @card(1),
    owns totalNeeded @card(1),
    owns createdAt @card(1),
    owns startDate @card(0..1),
    owns endDate @card(0..1),
    plays containsTask:task @card(1),
    plays requiresSkill:task @card(0..),
    plays registersForTask:task @card(0..);

entity skill,
    owns id @key,
    owns name @card(1) @unique, # was `name @key` before UUID migration
    owns isPending @card(1),
    owns createdAt @card(1),
    plays requiresSkill:skill @card(0..),
    plays hasSkill:skill @card(0..);

entity inviteKey,
    owns key @key,
    owns inviteType @card(1),
    owns isUsed @card(1),
    owns createdAt @card(1),
    plays businessInvite:key @card(0..1);

relation oauthAuthentication,
    relates provider,
    relates user,
    owns oauthSub @key;

relation creates,
    relates supervisor @card(1),
    relates project @card(0..),
    owns createdAt @card(1);

relation manages,
    relates supervisor @card(1),
    relates business @card(1),
    owns location @card(1..);

relation hasProjects,
    relates business @card(1),
    relates project @card(1..);

relation containsTask,
    relates project @card(1),
    relates task @card(1);

relation requiresSkill,
    relates task @card(1),
    relates skill @card(1);

relation hasSkill,
    relates student @card(1),
    relates skill @card(1),
    owns description @card(0..1);

relation registersForTask,
    relates student @card(1),
    relates task @card(1),
    owns description @card(1),
    owns isAccepted @card(0..1),
    owns response @card(0..1),
    owns createdAt @card(1),
    owns requestedAt @card(0..1),
    owns acceptedAt @card(0..1),
    owns startedAt @card(0..1),
    owns completedAt @card(0..1);

relation businessInvite,
    relates business @card(1),
    relates key @card(1);

entity portfolioItem,
    owns id @key,
    owns timelineSnapshot @card(1),
    owns projectSnapshot @card(1),
    owns taskSnapshot @card(1),
    owns skillsSnapshot @card(1),
    plays hasPortfolio:item @card(1);

relation hasPortfolio,
    relates student @card(1),
    relates item @card(1);

# Deeltaken Systeem (Subtasks)
# UI toont "Deeltaak" - toegankelijk voor alle disciplines
entity subtask,
    owns id @key,
    owns title @card(1),
    owns subtaskWhat @card(0..1),      # WAT: Wat moet er gebeuren?
    owns subtaskWhy @card(0..1),       # WAAROM: Context en reden
    owns subtaskHow @card(0..1),       # HOE: Aanpak/stappen
    owns subtaskCriteria @card(0..1),  # CRITERIA: Waar moet het aan voldoen?
    owns subtaskStatus @card(1),       # "open" | "in_progress" | "done"
    owns createdAt @card(1),
    owns completedAt @card(0..1),
    plays belongsToTask:subtask @card(1),
    plays claimedBy:subtask @card(0..1);

# Templates voor veelvoorkomende deeltaken
entity subtaskTemplate,
    owns id @key,
    owns templateName @card(1),
    owns title @card(0..1),
    owns subtaskWhat @card(0..1),
    owns subtaskWhy @card(0..1),
    owns subtaskHow @card(0..1),
    owns subtaskCriteria @card(0..1),
    owns createdAt @card(1),
    plays belongsToBusiness:template @card(1);

relation belongsToTask,
    relates task @card(1),
    relates subtask @card(0..);

relation claimedBy,
    relates subtask @card(1),
    relates student @card(0..1);

relation belongsToBusiness,
    relates business @card(1),
    relates template @card(0..);

#Werkt helaas nog niet :(
# TLDR: maakt automatisch een permission relatie aan als een supervisor een project aanmaakt permissie voor wat? zien we dan wel maar wss aanpassingen doen etc
# nu ik erover nadenk moet dit eigenlijk gebeuren met projectCreation.. TODO: apply rule for projectCreation relation
#rule access: when {
#    (supervisor: $s, project: $p) isa projectCreation;
#} then {
#    (supervisorRole: $s, projectRole: $p) isa accessPermission;
#};


attribute email value string;
attribute name value string;
attribute fullName value string;
attribute imagePath value string;
attribute cvPath value string;
attribute id value string;
attribute oauthSub value string;
attribute description value string;
attribute location value string;
attribute country value string;
attribute isPending value boolean;
attribute createdAt value datetime-tz;
attribute totalNeeded value integer;
attribute isAccepted value boolean;
attribute response value string;
attribute key value string;
attribute inviteType value string @values("business", "teacher");
attribute isUsed value boolean;
attribute sector value string;
attribute companySize value string;
attribute website value string;
attribute isArchived value boolean;
attribute requestedAt value datetime-tz;
attribute acceptedAt value datetime-tz;
attribute startedAt value datetime-tz;
attribute completedAt value datetime-tz;
attribute timelineSnapshot value string;
attribute projectSnapshot value string;
attribute taskSnapshot value string;
attribute skillsSnapshot value string;
attribute startDate value datetime-tz;
attribute endDate value datetime-tz;
attribute title value string;
attribute subtaskWhat value string;
attribute subtaskWhy value string;
attribute subtaskHow value string;
attribute subtaskCriteria value string;
attribute subtaskStatus value string @values("open", "in_progress", "done");
attribute templateName value string;
